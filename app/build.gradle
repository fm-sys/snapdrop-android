apply plugin: 'com.android.application'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.fmsys.snapdrop"
        minSdkVersion 21
        targetSdkVersion 30

        // ToDo list - Update Instruction
        //  -   As first step, always check if unmerged crowdin updates are available and **pull** these changes into the local repo!
        //  -   Change version code and name when releasing a new version!
        //  -   Afterwards, build a new version via Build > Generate Signed Bundle / APK...
        //  -   Please move the APK to the releases/old folder for archiving purposes
        //  -   Push to GitHub
        //  -   Write Changelog at GitHub and add APK
        //  -   Upload and publish APK to Google Play
        //  -   Publish the release tag on Github
        //  -   Post release info to Twitter
        //  -   Optional: At last the output-metadata.json can be moved to the docs folder to trigger the in-app update notification.

        versionCode 10
        versionName "1.5.3"
        applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "snapdrop_v${defaultConfig.versionName}.apk"
            }
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }

    // use Java 8 to make use of lamda-expressions, etc.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.webkit:webkit:1.4.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.squareup.okhttp:okhttp:2.7.5'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation "com.anggrayudi:storage:0.5.2"

}

/*
 * own checkstyle configuration
 * Android plugin doesn't interoperate with the Checkstyle plugin
 */
apply plugin: 'checkstyle'

tasks.register('checkstyle', Checkstyle) {
    configFile file("${project.rootDir}/checkstyle.xml")
    source 'main/java'
    source '../test/java'
    source '../androidTest/java'
    include '**/*.java'

    classpath = files()
}

tasks.named('check') {
    dependsOn tasks.named('checkstyle')
}
